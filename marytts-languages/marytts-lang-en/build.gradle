plugins {
    id 'de.dfki.mary.component' version '0.1'
}
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/publishLogic.gradle"

marytts {
    component {
        name = 'English'
        packageName = 'marytts.language.en'
        configBaseClass = 'LanguageConfig'
        config from: 'component.yaml'
    }
}

repositories {
    ivy {
        url 'http://opennlp.sourceforge.net'
        patternLayout {
            artifact 'models-[revision]/[module].[ext]'
        }
    }
}

configurations {
    opennlp
}

dependencies {
    compile project(':marytts-runtime')
    opennlp group: 'org.apache.opennlp', name: 'en-pos-maxent', version: '1.5', ext: 'bin'
    runtime group: 'de.dfki.mary', name: 'marytts-lexicon-en_US-cmudict', version: '0.1.0'
    testCompile libs.xmlunit
    testCompile project(path: ':marytts-runtime', configuration: 'testCompile')
    integrationTestCompile project(':marytts-runtime').sourceSets.test.output
}

processResources {
    from configurations.opennlp, {
        into "marytts/language/en/tagger"
    }
}

task generateComponentXmlDescriptor(type: GenerateComponentXmlDescriptor) {
    locales = ['en-US', 'en-GB']
}

rootProject.distributions.main.contents {
    from generateComponentXmlDescriptor, {
        into 'installed'
    }
}
